/*
 * This .xtext file defines the grammar of the plugin.
 * Using XText, this grammar enables the user to get the functionality 
 * of full-editor with color coding, auto-formatting and quick fixes.
 */
 
/*
 * import terminals
 */
grammar org.xtext.osy.ExtendedSMV with org.eclipse.xtext.common.Terminals

/*
 * import basic types
 */

import 'http://www.eclipse.org/emf/2002/Ecore' as ecore

generate extendedSMV "http://www.xtext.org/osy/ExtendedSMV"

/*
 * Model is the base of every .xtext file, this is the root of the tree
 * In NuSMV, every model can include some modules,
 * No imports, as in NuSMV
 */
Model:
	(modules += Module)*
;

/*
 * module declaration
 * with parameters that the module gets as input 
 */
Module:
	'MODULE' name=IDENTIFIER  
	('(' ((params += Parameter) (',' params += Parameter)*)? ')')?
	(sections += Section)*
;

Parameter:
	name = IDENTIFIER
;

/*
 * this terminal purpose is to include more characters than ID,
 * and therefore enables more identifiers 
 */
terminal IDENTIFIER:
	(('a' .. 'z') |( 'A' .. 'Z') | '_') (('a' .. 'z') | ('A' .. 'Z') | '_' | '0' | '1' | '2' | '3' | '4' | '5' | '6' | '7' | '8' |'9' | '$' | '#' | '-')* 
;

terminal SL_COMMENT : '--' !('\n'|'\r')* ('\r'? '\n')? ;

/*
 * A section is every sub part in a module
 */
Section:
	VariableDeclaration | Assignments | DefineDeclaration | Constraints
	| LTLSpecification | CTLSpecification | PatternsDefinitions
;

PatternsDefinitions:
	'PATTERNS'
	(macros += MacroDeclaration)+
;

MacroDeclaration:
	MacroDeclaration_1_var  | 
	MacroDeclaration_2_vars | 
	MacroDeclaration_3_vars 
; 

MacroDeclaration_1_var:
	name=IDENTIFIER '(' var1=IDENTIFIER  ')' ':' macroPattern = PATTERN  '=>'  macroInterpretation = PATTERN  ';'
;

MacroDeclaration_2_vars:
	name=IDENTIFIER '(' var1=IDENTIFIER ',' var2=IDENTIFIER ')' ':' macroPattern = PATTERN  '=>'  macroInterpretation = PATTERN  ';'
;

MacroDeclaration_3_vars:
	name=IDENTIFIER '(' var1=IDENTIFIER ',' var2=IDENTIFIER ',' var3=IDENTIFIER ')' ':' macroPattern = PATTERN  '=>'  macroInterpretation = PATTERN  ';'
;

/*
 * Define variables section
 */
VariableDeclaration:
  {VariableDeclaration}
  'VAR' (variables+=DeclaredVariable)*
;

DeclaredVariable returns Variable:
  name=IDENTIFIER ':' type=Type semicolon?=';'?
;

/*
 * Type can be also a model identifier, in case that it was declared 
 */
Type: 
  SimpleTypeSpecifier
  | ModuleTypeSpecifier 
;

/*
 * a module type can be one that was declared only, 
 * no option for declaring a module inside type specifying.
 */
SimpleTypeSpecifier:
	{BooleanType} 'boolean'
	| {WordType} ('word' '[' wordINT=INT ']') 
	| {UnsignedWordType} ('unsigned' 'word' '[' uWordINT=INT ']')
	| {SignedWordType} ('signed word' '[' signedINT=INT ']')
	| {EnumType} '{' literals+=EmunLiteral (',' literals+=EmunLiteral)* '}'
	| {IntervalType} lower=INT '..' upper=INT
	| {ArrayType} 'array' lower=INT '..' upper=INT 'of' type=SimpleTypeSpecifier
;

ModuleTypeSpecifier:
  {SyncrProcessType} module=[Module|IDENTIFIER] ("(" params+=SimpleExpression ("," params+=SimpleExpression)* ")")?
  | {AsyncrProcessType} "process" module=[Module|IDENTIFIER] ("(" params+=SimpleExpression ("," params+=SimpleExpression)* ")")?
;

EmunLiteral:
	EnumVariable
	| IntegerConstant
	| BooleanConstant
;

EnumVariable:  
  name=IDENTIFIER
;

IntegerConstant returns Expression:
  {IntegerConstant} constant=INT
;

DefineDeclaration:
  {DefineDeclaration}
  'DEFINE' (definitions+=Definition)*
;

Constraints:  
  feature=BasicOpConstraints expression=SimpleExpression semicolon?=';'?
;

BasicOpConstraints:
  'INIT'|'INVAR'|'TRANS'|'FAIRNESS'|'JUSTICE';

SimpleExpression returns Expression: 
	BasicExpression
;

SetBodyExpression returns Expression:
  {SetBodyExpression}
  '{' expressions+=SimpleExpression (',' expressions+=SimpleExpression)* '}'
;

/*
 * Assign section 
 */	
Assignments:
	{Assignments}
	'ASSIGN'
	(assignments += AssignmentExpression)*
;

AssignmentExpression:
  SimpleAssign
  | InitAssign
  | NextAssign
;

SimpleAssign:
  id=ComplexIdentifier ':=' expression=SimpleExpression semicolon?=';'?
;

InitAssign:
  'init' '(' id+=IDENTIFIER ('.' id+=IDENTIFIER)*  ')' ':=' expression=SimpleExpression semicolon?=';'?
;

/*
 * this rule could be "SimpleIdentifier", but this way enables the writer
 * to extend the essence in future versions without harm other parts of the grammar. 
 */
NextAssign:
  'next' '(' id+=IDENTIFIER ('.' id+=IDENTIFIER)* ')' ':=' expression=NextExpression semicolon?=';'?
;

NextExpression returns Expression:
  SimpleExpression
;

BOOL:
	'TRUE' | 'FALSE'
;
	
/*
 * A BasicExpression is one of the most basic stones of the grammar.
 * it works in a manner such that it at first looks for a code that is suitable
 * for Imply, the code that suitable for Equivalence, etc 
 * (the order is derived from the order of the rules)
 * 
 */
BasicExpression returns Expression:
  Implies;


Implies returns Expression:
  Equivalence ({BinaryExpression.left=current} feature=OpImplies right=Equivalence)*; 
  
OpImplies:
 '->';


Equivalence returns Expression:
  IfThenElse ({BinaryExpression.left=current} feature=OpEquivalence right=IfThenElse)*;
  
OpEquivalence:
 '<->';
  
IfThenElse returns Expression:
  Or ({IfThenElse.left=current}'?' then=Or ':' else=Or)? 
;
  
Or returns Expression:
  And ({BinaryExpression.left=current} feature=OpOr right=And)*;

OpOr:
  'xnor' | '|' | 'xor';

And returns Expression:
  BasicRelational ({BinaryExpression.left=current} feature=OpAnd right=BasicRelational)*;
  
OpAnd:
  '&';
 
BasicRelational returns Expression:
  BasicIn ({BinaryExpression.left=current} feature=BasicOpRelational right=BasicIn)*;

BasicOpRelational:
  '=' | '!=' | '<' | '>' | '<=' | '>=';

BasicIn returns Expression:
  BasicUnion ({BinaryExpression.left=current} feature=BasicOpIn right=BasicUnion)*;

BasicOpIn:
 'in';
  
BasicUnion returns Expression:
  BasicShift ({BinaryExpression.left=current} feature=BasicOpUnion right=BasicShift)*;

BasicOpUnion:
 'union';

BasicShift returns Expression:
  BasicAddSub ({BinaryExpression.left=current} feature=BasicOpShift right=BasicAddSub)*;  

BasicOpShift:
  '<<'|'>>';

BasicAddSub returns Expression:
  BasicRemainder ({BinaryExpression.left=current} feature=BasicOpAddSub right=BasicRemainder)*;  

BasicOpAddSub:
  '+'|'-';

BasicRemainder returns Expression:
  UnaryMinusExpression ({BinaryExpression.left=current} feature=BasicOpRemainder right=UnaryMinusExpression)*;
  
BasicOpRemainder:
  'mod' | '*' | '/';  

UnaryMinusExpression returns Expression:
	BasicWordConcatenation | {UnaryMinusExpression} feature=BasicMinusOpUnary expression=UnaryMinusExpression
;

BasicMinusOpUnary:
	'-'
;

BasicWordConcatenation returns Expression:
   UnaryExclamationExpression ({BinaryExpression.left=current} feature=BasicOpWordConcatenation right=UnaryExclamationExpression)*;

BasicOpWordConcatenation:
  '::';
  
UnaryExclamationExpression returns Expression:
	UnaryExpression | {UnaryExclamationExpression} feature=BasicExclamationOpUnary expression=UnaryExclamationExpression
;

BasicExclamationOpUnary:
	'!'
;

UnaryExpression returns Expression:
	SimpleExpressionParenthesis | Case | BooleanConstant | SetBodyExpression | EnumIdentifier
	| Next | {Keywords} ('self' | 'running') 
;

Next returns Expression:
  {Next}
  'next' '(' expression=SimpleExpression ')'
;  

SimpleExpressionParenthesis returns Expression:
  '(' Implies {SimpleExpressionParenthesis.expression=current} ')'
;


EnumIdentifier:
	name += GeneralIdentifier ('.' name += GeneralIdentifier)*
;

GeneralIdentifier:
	INT | IDENTIFIER 
;


BooleanConstant returns Expression: 
  {BooleanConstant} 
  (isTrue?='TRUE'|'FALSE')
;

Case returns Expression:
  {Case}
  'case'
    (caseBodies+=CaseBody)+
  'esac'
;

CaseBody returns Expression:
  {CaseBody}
  case = SimpleExpression ':' assigment=SimpleExpression ';'
;

//Specs:

LTLSpecification:
	'LTLSPEC' ((expression=LTLExpression ('&' patterns+=PATTERN)*) | (patterns+=PATTERN)+) ';'?
;

CTLSpecification:
	'CTLSPEC' ((expression=CTLExpression ('&' patterns+=PATTERN)*) | (patterns+=PATTERN)+) ';'?
;

CTLExpression returns Expression:
	SpecImplies 
;

LTLExpression returns Expression: 
	SpecImplies 
;

SpecImplies returns Expression:
  SpecEquivalence ({BinaryExpression.left=current} feature=OpImplies right=SpecEquivalence)*
;
  
SpecEquivalence returns Expression:
  SpecOr ({BinaryExpression.left=current} feature=OpEquivalence right=SpecOr)*
;  
  
SpecOr returns Expression:
  SpecAnd ({BinaryExpression.left=current} feature=OpOr right=SpecAnd)*
;


SpecAnd returns Expression:
  Triggered ({BinaryExpression.left=current} feature=OpAnd right=Triggered)*;
  
Triggered returns Expression:
  Since ({BinaryExpression.left=current} feature=OpTriggered right=Since)*;
  
OpTriggered:
  'T';

Since returns Expression:
  Releases ({BinaryExpression.left=current} feature=OpSince right=Releases)*;
  
OpSince:
  'S';


Releases returns Expression:
  Until ({BinaryExpression.left=current} feature=OpReleases right=Until)*;

OpReleases:
  'V';


Until returns Expression:
  SpecUnaryExpression ({BinaryExpression.left=current} feature=OpUntil right=SpecUnaryExpression)*;
  
OpUntil:
  'U';
  
SpecUnaryExpression returns Expression:
	ExistsUntil | ForallUntil | 
	{UnaryExpression} feature=OpSpecUnaryExpression expression=SpecUnaryExpression |
	Primary 
;

OpSpecUnaryExpression:
	'!' 
	|'X'|'G'|'F'|'Y'|'Z'|'H'|'O' // Used by LTL specifications
	|'EG'|'EX'|'EF'|'AG'|'AX'|'AF' // Used by CTL specifications  	
;

  
ExistsUntil returns Expression:
  {ExistsUntil}
  'E' '[' expression=SimpleExpression ']';
    
ForallUntil returns Expression:
  {ForallUntil}
  'A' '[' expression=SimpleExpression ']';

  
  
Primary returns Expression:
  Parenthesis
  | EnumAssignment
  | EnumIdentifier
;

terminal PATTERN:
	 '"' -> '"'
;


EnumAssignment:
	var += IDENTIFIER ('.' var+= IDENTIFIER)* '=' var2 = (IDENTIFIER | BOOL)
;

Parenthesis returns Expression:
  '(' SpecImplies {Parenthesis.expression=current} ')'
;

ComplexIdentifier returns Expression:
  ComplexIdentifierPrimary ({BinaryExpression.left=current} '.' right=ComplexIdentifierPrimary)*;
  
ComplexIdentifierPrimary returns Expression:  
  {VariableIdentifier} var=[Variable|IDENTIFIER] ('[' expression=SimpleExpression ']')?  
  | {PredefinedVariable} feature=('self'|'running')
;

Definition returns Variable:
  name=IDENTIFIER ':=' assignment=SimpleExpression semicolon?=';'?
;
